apply plugin: 'java'
apply plugin: 'pmd' 
apply plugin: 'nexus'
apply plugin: 'release'
apply plugin: 'findbugs'
apply plugin: 'com.github.nwillc.vplugin'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

group = "com.github.nwillc"
archivesBaseName = "almost-functional"
version = "1.7.9"
targetCompatibility = "1.6"
sourceCompatibility = "1.6"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12',
                'org.assertj:assertj-core:1.7.1',
                'com.github.nwillc:contracts:1.6.6'
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
              url 'http://maven.tmatesoft.com/content/repositories/releases/'
            }
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1',
                    'org.tmatesoft.sqljet:sqljet:1.1.9',
                    'au.com.ish.gradle:release:2.2.2',
                    'com.github.nwillc:vplugin:1.2',
                    'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.1.0'
    }
}

release {
  failOnSnapshotDependencies = true
  allowLocalModifications = true
  releaseDryRun = false
  scm = 'git'
}

nexus {
    attachSources = true
    attachTests = false
    attachJavadoc = true
    sign = true
}

modifyPom {
    project {
        name 'almost functional'
        description 'Almost Functional is very low impact functional classes inspired by guava, jdk 1.8 and various others.'
        url 'https://github.com/nwillc/almost-functional'
        inceptionYear '2014'

        scm {
            url 'https://github.com/nwillc/almost-functional'
            connection 'scm:https://nwillc@github.com:nwillc/almost-functional.git'
            developerConnection 'scm:git:git@github.com:nwillc/almost-functional.git'
        }

        licenses {
            license {
                name 'ISC'
                url 'http://www.isc.org/downloads/software-support-policy/isc-license/'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'nwillc'
                name 'N Christopher'
                email 'nwillc@gmail.com'
            }
        }
    }
}

findbugs {
    toolVersion = '2.0.3'
    ignoreFailures = true
}


jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    toolVersion = '4.3'
    sourceSets = [sourceSets.main]
    ignoreFailures = true
    ruleSets = [
            "basic",
            "braces",
            "naming",
            "clone",
            "codesize",
            "controversial",
            "design",
            "finalizers",
            "imports",
            "junit",
            "logging-java",
            "migrating",
            "optimizations",
            "strictexception",
            "strings",
            "sunsecure",
            "typeresolution",
            "unusedcode"
            ]
}

