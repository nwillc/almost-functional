/*
 * Copyright (c) 2015, nwillc@gmail.com
 *
 * Permission to use, copy, modify, and/or distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 *
 */

buildscript {
    repositories {
        mavenLocal()
        jcenter {
            url 'http://jcenter.bintray.com'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        maven {
            url 'http://maven.tmatesoft.com/content/repositories/releases/'
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4',
                'org.tmatesoft.sqljet:sqljet:1.1.9',
                'au.com.ish.gradle:release:2.2.2',
                'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.9'
    }
}

apply plugin: "java"
apply plugin: 'maven'
apply plugin: "jacoco"
apply plugin: "pmd"
//apply plugin: "com.github.nwillc.vplugin"
apply plugin: 'release'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'info.solidsoft.pitest'

repositories {
    mavenLocal()
    jcenter {
        url 'http://jcenter.bintray.com'
    }
    maven {
        url "http://dl.bintray.com/nwillc/maven"
    }
}

dependencies {
    testCompile 'junit:junit:4.12',
            'org.assertj:assertj-core:1.7.1',
            'com.github.nwillc:jdk_contract_tests:1.7.6'
}

group = "com.github.nwillc"
archivesBaseName = "almost-functional"
version = "1.9.8-SNAPSHOT"
targetCompatibility = "1.6"
sourceCompatibility = "1.6"

release {
    failOnSnapshotDependencies = true
    allowLocalModifications = true
    releaseDryRun = false
    scm = 'git'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    toolVersion = '4.3'
    sourceSets = [sourceSets.main]
    ignoreFailures = true
    ruleSets = [
            "basic",
            "braces",
            "naming",
            "clone",
            "codesize",
            "controversial",
            "design",
            "finalizers",
            "imports",
            "junit",
            "logging-java",
            "migrating",
            "optimizations",
            "strictexception",
            "strings",
            "sunsecure",
            "typeresolution",
            "unusedcode"
    ]
}

test {
    beforeTest { descriptor ->
        logger.lifecycle("\tRunning " + descriptor.className + "." + descriptor.name)
    }

    afterSuite { descriptor, result ->
        if (descriptor.parent == null) {
            logger.lifecycle("\tTests run: " + result.testCount
                    + ", Failures: " + result.failedTestCount
                    + ", Skipped: " + result.skippedTestCount)
        }
    }

    testLogging {
        exceptionFormat "full"
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    dryRun = false
    publish = true
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'Java 1.6+ compatible light weight functional bits inspired by guava and 1.8'
        websiteUrl = 'https://github.com/nwillc/almost-functional'
        issueTrackerUrl = 'https://github.com/nwillc/almost-functional/issues'
        vcsUrl = 'https://github.com/nwillc/almost-functional.git'
        licenses = ['ISC']
        labels = ['jdk6', 'functional']
        publicDownloadNumbers = true
    }
}

bintrayUpload.onlyIf { !project.version.toString().endsWith('SNAPSHOT') }

pitest {
    targetClasses = ['almost.functional.*']  //by default "${project.group}.*"
    //pitestVersion = "1.1.0" //not needed when a default PIT version should be used
    threads = 4
    outputFormats = ['XML', 'HTML']
}